/**
 * A library that supports various program execution services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() {
        do Memory.init();
        do Math.init();
        do Output.init();
        do Screen.init();
        do Keyboard.init();
        do Main.main();
        do Sys.halt();
        return;
    }

    /** Halts the program execution. */
    function void halt() {
        while(true){
        }
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int times, i, j;
        if((duration < 0) | (duration = 0)){
            return;
        }
        let times = 100;
        let i = 0;
        while(i < duration){
            let j = 0;
            while(j < times){
                let j = j + 1;
            }
            let i = i + 1;
        }
        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        do Screen.clearScreen();
        do Output.moveCursor(11, 29);
        do Output.printString("ERR<");
        do Output.printInt(errorCode);
        do Output.printString(">");
        do Sys.halt();
        return;
    }
}